# Maintainer: Veeti Paananen <veeti.paananen@rojekti.fi>
pkgname=entr
pkgver=2.9
pkgrel=1
pkgdesc="Run arbitrary commands when files change"
arch=('i686' 'x86_64')
url="http://entrproject.org/"
license=('MIT')
source=("http://entrproject.org/code/entr-$pkgver.tar.gz")
sha256sums=('9fa4a06f92dc5aaed9d822d80400f96cd46ee4e0909f59e31c3c3d91d9f301c1')

# bad tar
_srcdir='eradman-entr-a0fcbd830bd9'

build() {
  cd "$srcdir/$_srcdir"

  ./configure
  make
}

check() {
  cd "$srcdir/$_srcdir"

  make -k test
}

package() {
  cd "$srcdir/$_srcdir"

  make PREFIX="$pkgdir/usr" install
  install -D -m 644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
